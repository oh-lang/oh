heap_array[capacity: hoisted_primitive, of]: all_of
[   @private m:
    [   Allocation[of, capacity];
        Count; count[wrapping: capacity]
    ]
    array[of]
]
{   
    ;;enscope(Arguments[of].): null
        Allocation capacity(Arguments count()) ?? panic()
        while Count < Arguments count()
            Allocation move_construct(In_bounds: Arguments[Count++]!)

    ;;descope(): null
        M clear(Drop_capacity)

    ::capacity(): count
        M Allocation capacity()

    ;;capacity(Capacity.): hm[capacity]
        while M Count > Capacity
            @Unused Of: M pop()
        M Allocation capacity(Capacity)

    ::count(): count
        M Count

    ;;append(Of.): hm[null]
        debug assert(M Count < count::Max)
        M capacity(At_least: M Count + 1) assert()
        Allocation move_construct(M Count++, Of!)
   
    # in-bounds reference, which is unsafe to call unless `In_bounds` is 
    # within the range `[0, M count())`.
    :;[In_bounds.]: (Of:;)
        debug assert(In_bounds >= 0 and In_bounds < M count())
        Allocation[In_bounds_and_constructed. In_bounds]

    ;;pop()?: of
        if M Count == 0
            Null
        else
            M Allocation read_out(--M Count)
}
