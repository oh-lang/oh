non_local_array_{of_:, count_: select_count_ = arch_ count_}: all_of_
{    @private m:
     [    allocation{of_, count_};
          count;
     ]
     array{of_};
}
{    # constructs an array `[1, 2, 3]` via syntax `non_local_array_(1, 2, 3)`,
     # inferring e.g., `t: int`.
     g_(arguments{~t_}.): hm_{g_{t_}}
          allocation{of_};
          count: arguments count_()
          allocation capacity_(count) assert_()
          count each index:
               allocation move_construct_(in_bounds: index, arguments[index]!)
          [allocation, count]

     ;;descope_(): null_
          m clear_(drop_capacity)

     ::capacity_(): capacity_
          m allocation capacity_()

     ;;capacity_(NEW_capacity.): hm_{null_}
          while m count > NEW_capacity
               _of: m remove_() ?? panic_("broken invariant")
          m allocation capacity_(NEW_capacity)

     ::count_(): count_
          m count

     ;;append_(of.): hm_{null_}
          REQUIRED_capacity: (m count + 1) assert_(er: out_of_memory)
          if REQUIRED_capacity > m capacity_()
               m capacity_(at_least: REQUIRED_capacity) assert_()
          m allocation move_construct_(in_bounds: REQUIRED_capacity highest_index_(), of!)
          m count = REQUIRED_capacity

     # in-bounds reference, which is unsafe to call unless `in_bounds` is 
     # within the range `[0, m count_())`.
     :;[in_bounds.]: (of:;)
          # TODO: `in_bounds` can probably be a u64 so we don't have to check < 0.
          debug assert_(in_bounds >= 0 and in_bounds < m count)
          allocation[in_bounds_and_constructed. in_bounds]

     ;;remove_()?: of
          if m count == 0
               null
          else
               m allocation read_out_(in_bounds: m count highest_index_())
               --m count

     capacity_: count_
}

[er, hm]: \/../container
