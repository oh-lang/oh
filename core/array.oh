[allocation]: \/allocation

er: flatten
[   \/allocation er
    \/indexable er
]
hm[ok]: hm[ok, er]

array[of, capacity: count]: all_of
[   hashable
    indexable_sequence[of]
]
{   heap_or_local[Local_bytes, locality: hoisted_primitive = h16]:
            heap_or_local_array[Local_bytes, locality, of]
    heap[capacity: hoisted_primitive = h64]: heap_array[capacity, of]

    ::capacity(): count

    ;;capacity(Capacity.): null 

    ::count(): count
        My Count

    ;;count[Require: of is defaultable](@New Count): hm[count]
        @Old Count: My Count
        if @New Count == @Old Count
            return ok(@Old Count)
        if @New Count > @Old Count
            My capacity(At_least: Count) assert()
            while My Count < @New Count
                append(of())
        else
            assert(@New Count >= 0, er Invalid_count)
            while My Count > @New Count
                @Unused Of: I pop()
        ok(@Old Count)

    clear: one_of[Keeping_capacity, Dropping_capacity]
    {   @alias Keep_capacity: Keeping_capacity
        @alias Drop_capacity: Dropping_capacity
    }

    ;;clear(Clear: clear = Keeping_capacity): null
        while My Count > 0
            @Unused Of: I pop()
        what One_of
            Keeping_capacity
                pass
            Dropping_capacity
                My capacity(0)

    ;;append(Of.): hm[null]
   
    :;[In_bounds.]: (Of:;)

    # moves and resets the value at this index.
    ;;[Index.]!?: of
        what My in_bounds(Index)
            Ok.
                My[Ok]!
            @Ignore Er.
                Null

    ;;[In_bounds.]!: of

    ;;pop()?: of
        # TODO: can implement here in terms of moot
    
    # returns a value of the array element at the specified index,
    # or the end of the array if not specified.  returns null if the element
    # would be out of bounds (e.g., `Index >= My count()`).
    ;;pop(Index.)?: of
        # don't resize the array in case `Index >= My Count`.
        what My in_bounds(Index)
            # TODO: shouldn't this be `Ok. in_bounds` if we're going to be pedantic
            #       with naming?  otherwise we shouldn't use `@Named Ok` in the definition
            #       of `hm`, and it would just be `In_bounds.`
            Ok.
                I pop(Ok)
            @Ignore Er.
                Null
        # alternatively: `My in_bounds(Index) map((Ok.): I pop(Ok), (@Unused Er.): Null)`

    ;;pop(In_bounds.): of
        debug assert(In_bounds >= 0 and In_bounds < My count())
        Result: My[In_bounds]!
        I erase(In_bounds)
        Result

    ;;erase(Index.): capacity
        if My in_bounds(Index) is Ok.
            I erase(Ok)
        else
            0

    ;;erase(In_bounds.): capacity
        debug assert(In_bounds >= 0 and In_bounds < My count())
        range(In_bounds, My count() - 1) each Destination_index:
            My[Destination_index] = My[In_bounds: Destination_index + 1]!
        I pop()
        1

    !!(Me): bool
        count(Me) > 0

    # returns a copy of this array, but sorted.
    # only makes sense if `of` is orderable.
    ::sort[Require: of is orderable](): you

    # sorts this array in place.
    # only makes sense if `of` is orderable.
    ;;sort[Require: of is orderable](): null

    # returns a sorted copy with a custom comparator that acts on a single element at a time.
    # `fn(By)` should be easy to calculate on the fly, otherwise you should run with `Tabulate`.
    # the result type `t` should be easily comparable via `<`, `<=`, etc.
    # examples:
    #   Array sort((By): By count())    # results in ["f", "de", "abc", "ghij"]
    #   Array::sort($By count())        # same, but without the lambda $
    ::sort[Require: t is orderable](fn(By: of): ~t): you

    # sorts in place with a custom comparator that acts on a single element at a time.
    # `fn(By)` should be easy to calculate on the fly, otherwise you should run with `Tabulate`.
    # the result type `t` should be easily comparable via `<`, `<=`, etc.
    # examples:
    #   Array; ["abc", "de", "f", "ghij"]
    #   Array sort($By count())         # `Array` becomes ["f", "de", "abc", "ghij"]
    #   Array;;sort((By): By count())   # same, but without the lambda $
    ;;sort[Require: t is orderable](fn(By: of): ~t): null

    # sorts using a tabulated work array (additional `O(N * size_of(t))` memory cost).
    # should be called in the case `fn(By)` is expensive to compute.
    :;sort[Require: t is orderable]
    (   fn(By: of): ~t, Tabulated. one_of[Tabulated]
    ):  respectively[you, null]

    # sorts with a custom comparator that acts on two elements at a time.
    # without certain transitive properties, the result's order may not make sense.
    :;sort(fn(Left: of, Right: of): comparison): respectively[you, null]

    ::hash(~Builder;):
    {   Builder hash(of)  # make array hashes distinct based on type.
        My each (Index., Of:)
            Builder hash(Index)
            Builder hash(Of)
    }

    ;;capacity(At_least: count): hm[null]
        Current_capacity: My capacity()
        if At_least <= Current_capacity 
            return ok()
        My capacity(Current_capacity double_or_max(At_least. 4))
}
