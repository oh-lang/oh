array_{of_, capacity_: select_count_ = arch_ count_}: all_of_
{    indexable_sequence{of_};
     # we could make this `hashable_` if `of_` is hashable, but we'll
     # implicitly make it hashable (by defining a `::hash_` method)
     # and only add to hashable's lookup table as needed.
}
{    index_: count_ signed_

     # sets this array's capacity to exactly `max_(m count_(), capacity)`;
     # i.e., no elements would get deleted by this operation.
     # prefer `array capacity_(at_least. count)` because it will grow the array
     # in an amortized way if you are increasing the capacity over time.
     ;;capacity_(capacity.): hm_{null_}

     :;[in_bounds.]: (of:;)

     ;;append_(of.): hm_{null_}
          m capacity_(at_least: m count_() + 1) assert_()
          m append_(of, assuming. capacity)

     # called as `array append_(3, assuming. _ capacity)`
     # TODO: `array append_(3, assuming_ capacity)` might be nicer
     ;;append_(of., assuming. one_of_{capacity:}): null_

     ::count_(): count_

     ;;count_{require: of_ is defaultable_}(NEW_count.): hm_{null_}
          OLD_count: m count_()
          if NEW_count == OLD_count
               return ok_()
          elif NEW_count > OLD_count
               capacity_(at_least_: NEW_count) assert_()
               while m count_() < NEW_count
                    append_(of_(), assuming. capacity)
          else
               assert_(NEW_count >= 0, er invalid_count)
               while m count_() > NEW_count
                    UNUSED_of: m pop_()

     ::capacity_(): count_

     ;;capacity_(at_least. count_): hm_{null_}
          current_capacity: m capacity_()
          if at_least <= current_capacity
               return ok_()
          doubled_capacity: current_capacity double_or_max_()
          m capacity_(max_(at_least, doubled_capacity))

     # call as `array clear_(keeping. _ no_capacity)` to deallocate
     # or `array clear_(keeping. _ same_capacity)` to keep the allocation.
     # TODO: `array clear_(keeping_ no_capacity)` might be nicer
     ;;clear_(keeping. one_of_{no_capacity:, same_capacity:}): null_
          while m count > 0
               UNUSED_of: m pop()
          what keeping
               same_capacity
                    pass
               no_capacity
                    m capacity_(0)

     ::[index.]?: (of:)
          # TODO: use m[in_bounds]

     ;;[index.]: hm_{(of;)}
          # TODO: use m[in_bounds]

     ;;pop_()?: of_

     # returns a value of the array element at the specified index,
     # or the end of the array if not specified.  returns null if the element
     # would be out of bounds (e.g., `index >= m count_()`).
     ;;pop_(index.)?: of_
          # don't resize the array in case `index >= m count`.
          what m in_bounds_(index)
               ok.
                    m pop_(ok)
               IGNORE_er.
                    null
          # alternatively: `m in_bounds_(index) map_({m pop_($ok)}, {$_er, null})`

     ;;pop_(in_bounds.): of_
          debug assert_(in_bounds < m count_())
          result: m[in_bounds]!
          m erase_(in_bounds)
          result

     # returns the number of elements erased
     ;;erase_(index.): count_
          if m in_bounds_(index) is ok.
               m erase_(ok)
          else
               0

     ;;erase_(in_bounds.): count_(1)
          debug assert_(in_bounds < m count_())
          range_(in_bounds, m count_() - 1) each DESTINATION_in_bounds:
               m[DESTINATION_in_bounds] = m[in_bounds: DESTINATION_in_bounds + 1]!
          m pop_()
          1

     # returns a copy of this array, but sorted.
     ::sort_{require: of_ is orderable_}(): o_

     # sorts this array in place.
     ;;sort_{require: of_ is orderable_}(): null_

     #[#
     sorts with a custom comparator that acts on a single element at a time.
     `fn_(by: of_): ~t_` should be easy to calculate on the fly, otherwise
     you should run `tabulated`.  the result type `t_` should be easily
     comparable via `<`, `<=`, etc.  examples:
          ```
          array: ["abc", "de", "f", "ghij"]
          print_(array sort_((by): by count_()))  # prints ["f", "de", "abc", "ghij"]
          print_(array sort_({$by count_()}))     # same, but via lambda arguments
          ```
     #]#
     :;sort_{require: t_ is orderable_}
     (    fn_(by: of_): ~t_
     ):        respectively_{o_, null_}

     #[#
     sorts using a tabulated work array (with an additional cost of
     `O(m count_() * memory_(t_))`).  should be used in case `fn_(by)`
     is expensive to compute.  example:
          ```
          array: ["abc", "de", "f", "ghij"]
          # should print ["f", "de", "abc", "ghij"]:
          print_(array sort_({$tabulated_by count_()}))
          ```
     #]#
     :;sort_{require: t_ is orderable_}
     (    fn_(tabulated_by: of_): ~t_
     ):        respectively_{o_, null_}

     # sorts with a custom comparator that acts on two elements at a time.
     # without certain transitive properties, the result's order may not make sense.
     :;sort_(fn_(left: of_, right: of_): ordering_): respectively_{o_, null_}

     ::hash_{require: of_ is hashable_}(~builder;): null_
          m each of:
               builder hash_(of)

     maybe_local_{local_bytes_}:
               maybe_local_array_{of_, local_bytes_, locality_: count_}
     non_local_: non_local_array_{of_, count_}

     @alias heap_: non_local_
     @alias heap_or_local_: maybe_local_

     er_: CONTAINER_er_
     count_: capacity_
}

# for simplicity, rounds to multiples of 8 bytes.
local_bytes_: select_{16, 24, 32, 40, 48, 56, 64}

[hm_, CONTAINER_er_]: \/container
