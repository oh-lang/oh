[allocation]: \/allocation

er: flatten
[   \/allocation er
    \/indexable er
]
hm[ok]: hm[ok, er]

array[of]: all_of
[   hashable
    indexable_sequence[of]
    @private@
    [   Allocation; allocation
        Count; count = 0
    ]
]
{   heap_or_local[Local_bits: Bit_count]: heap_or_local_array[Local_bits, of]

    ;;enscope(Arguments[of].): null
        Allocation capacity(Arguments count()) ?? panic()
        while Count < Arguments count()
            Allocation move_construct(In_bounds: Arguments[Count++]!)

    ;;descope(): null
        I clear(Drop_capacity)

    ::capacity(): count
        My Allocation capacity()

    ;;capacity(Capacity): hm[capacity]
        while My Count > Capacity
            @Unused Of: I pop()
        My Allocation capacity(Capacity)

    ::count(): count
        My Count

    ;;count[Require: of is defaultable](@New Count): hm[count]
        @Old Count: My Count
        if @New Count == @Old Count
            return ok(@Old Count)
        if @New Count > @Old Count
            My capacity(At_least: Count) assert()
            while My Count < @New Count
                Allocation default_construct(In_bounds: My Count++)
        else
            assert(@New Count >= 0, er Invalid_count)
            while My Count > @New Count
                @Unused Of: I pop()
        ok(@Old Count)
    
    ;;clear(One_of. one_of[Keep_capacity, Drop_capacity] = Keep_capacity): null
        while My Count > 0
            @Unused Of: I pop()
        what One_of
            Keep_capacity
                pass
            Drop_capacity
                My Allocation capacity(0)

    ;;append(Of): hm[null]
        debug assert(My Count < count::Max)
        My capacity(At_least: My Count + 1) assert()
        Allocation copy_construct(My Count++, Of)

    ;;append(Of.): hm[null]
        debug assert(My Count < count::Max)
        My capacity(At_least: My Count + 1) assert()
        Allocation move_construct(My Count++, Of!)
   
    # in-bounds reference, which is unsafe to call unless `In_bounds` is 
    # within the range `[0, My count())`.
    :;[In_bounds.]: (Of:;)
        debug assert(In_bounds >= 0 and In_bounds < My count())
        Allocation[In_bounds_and_constructed. In_bounds]

    ;;pop()?: of
        if My Count == 0
            Null
        else
            My Allocation read_out(--My Count)
        
    # returns a value of the array element at the specified index,
    # or the end of the array if not specified.  returns null if the element
    # would be out of bounds (e.g., `Index >= My count()`).
    ;;pop(Index.)?: of
        # don't resize the array in case `Index >= My Count`.
        what My in_bounds(Index)
            # TODO: shouldn't this be `Ok. in_bounds` if we're going to be pedantic
            #       with naming?  otherwise we shouldn't use `@Named Ok` in the definition
            #       of `hm`, and it would just be `In_bounds.`
            Ok.
                I pop(Ok)
            Er.
                @ignore Er
                Null
        # alternatively: `My in_bounds(Index) map((Ok.): I pop(Ok), (@Unused Er.): Null)`

    ;;pop(In_bounds.): of
        debug assert(In_bounds >= 0 and In_bounds < My count())
        Result: My[In_bounds]!
        I erase(In_bounds)
        Result

    ;;erase(Index.): null
        if My in_bounds(Index) is Ok.
            I erase(Ok)

    ;;erase(In_bounds.): null
        debug assert(In_bounds >= 0 and In_bounds < My count())
        range(In_bounds, My count() - 1) each Destination_index:
            My[Destination_index] = My[In_bounds: Destination_index + 1]!
        My count(My count() - 1)

    !!(Me): bool
        count(Me) > 0

    # returns a copy of this array, but sorted.
    # only makes sense if `of` is orderable.
    ::sort[Require: of is orderable](): you

    # sorts this array in place.
    # only makes sense if `of` is orderable.
    ;;sort[Require: of is orderable](): null

    # returns a sorted copy with a custom comparator that acts on a single element at a time.
    # `fn(By)` should be easy to calculate on the fly, otherwise you should run with `Tabulate`.
    # the result type `t` should be easily comparable via `<`, `<=`, etc.
    # examples:
    #   Array sort((By): By count())    # results in ["f", "de", "abc", "ghij"]
    #   Array::sort($By count())        # same, but without the lambda $
    ::sort[Require: t is orderable](fn(By: of): ~t): you

    # sorts in place with a custom comparator that acts on a single element at a time.
    # `fn(By)` should be easy to calculate on the fly, otherwise you should run with `Tabulate`.
    # the result type `t` should be easily comparable via `<`, `<=`, etc.
    # examples:
    #   Array; ["abc", "de", "f", "ghij"]
    #   Array sort($By count())         # `Array` becomes ["f", "de", "abc", "ghij"]
    #   Array;;sort((By): By count())   # same, but without the lambda $
    ;;sort[Require: t is orderable](fn(By: of): ~t): null

    # sorts using a tabulated work array (additional `O(N * size_of(t))` memory cost).
    # should be called in the case `fn(By)` is expensive to compute.
    :;sort[Require: t is orderable]
    (   fn(By: of): ~t, Tabulated. one_of[Tabulated]
    ):  respectively[you, null]

    # sorts with a custom comparator that acts on two elements at a time.
    # without certain transitive properties, the result's order may not make sense.
    :;sort(fn(Left: of, Right: of): comparison): respectively[you, null]

    ::hash(~Builder;):
    {   Builder hash(of)  # make array hashes distinct based on type.
        My each (Index., Of:)
            Builder hash(Index)
            Builder hash(Of)
    }

    @private
    ;;capacity(At_least: count): hm[null]
        if At_least <= My capacity()
            return ok()

        # we should at least try to double the storage
        Try_to_allocate; I roughly_double_allocation()
        while Try_to_allocate > At_least
            if My Allocation count(Try_to_allocate) is_ok()
                return ok()
            # in case we can't allocate that much, we can back down.
            Try_to_allocate = (Try_to_allocate + At_least) // 2

        My Allocation count(At_least)

    @private
    ::roughly_double_allocation(): count
        if My Allocation Count == 0
            return 4
        Doubled: 2 * My Allocation Count
        if Doubled >= 0
            return Doubled
        # if we're at this point, we're in insane territory
        # TODO
}
