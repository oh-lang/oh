#array[#of:, #capacity: #select_count = arch #count]: #all_of
[    #indexable[#of, #count];
     # we could make this `#hashable` if `#of` is hashable, but we'll
     # implicitly make it hashable (by defining a `::hash` method)
     # and only add to hashable's lookup table as needed.
]
{    # index can be negative to support "from the back" logic.
     # NOTE: this can be null in case `#capacity` does not support negative indexing.
     #index: #count #index
     # always positive.
     #offset: #count #offset

     # sets this array's capacity to exactly `max(m count(), capacity, min_capacity)`;
     # i.e., no elements would get deleted by this operation, and there may be a minimum
     # capacity that will always be respected, e.g., for a local array.
     # prefer `array capacity(at_least. count)` because it will grow the array
     # in an amortized way if you are increasing the capacity over time.
     ;;capacity(capacity.): #hm[#null]

     :;[in_bounds.]: #(of:;)

     ;;append(of.): #hm[#null]
          m capacity(at_least: m count() + 1) assert()
          m append(of, assuming. capacity)

     #assuming: #one_of[enough_capacity:]
     # called as `array append(3, #assuming enough_capacity)`
     ;;append(of., assuming.): #null

     ::count(): #count

     ;;count[require: #of is #defaultable](NEW_count.): #hm[#null]
          OLD_count: m count()
          if NEW_count == OLD_count
               return #ok()
          elif NEW_count > OLD_count
               m capacity(at_least: NEW_count) assert()
               while m count() < NEW_count
                    m append(#of(), #assuming enough_capacity)
          else
               while m count() > NEW_count
                    UNUSED_of: m pop()

     ::capacity(): #count

     ;;capacity(at_least. #count): #hm[#null]
          current_capacity: m capacity()
          if current_capacity >= at_least
               return #ok()
          doubled_capacity: min(2 * current_capacity, #count max)
          m capacity(max(at_least, doubled_capacity))

     #keeping: #one_of[same_capacity:, no_capacity:]
     # call as `array clear(#keeping no_capacity)` to deallocate
     # or `array clear(#keeping same_capacity)` to keep the allocation.
     ;;clear(keeping.): #null
          while m count() > 0
               UNUSED_of: m pop()
          what keeping
               ~# same_capacity
                    pass
               ~# no_capacity
                    m capacity(0)

     ::[index.]?: #(of:)
          # TODO: use m[in_bounds]

     ;;[index.]: #hm[#(of;)]
          # TODO: use m[in_bounds]

     ;;pop()?: #of
          if m count() > 0
               of. m pop()
               of
          else
               null

     ;;pop(): #of

     # returns a value of the array element at the specified index,
     # or the end of the array if not specified.  returns null if the element
     # would be out of bounds (e.g., `select >= m count()`).
     ;;pop(select. #select[#index, #offset])?: #of
          # don't resize the array in case `index >= m count`.
          what m in_bounds(select)
               ok.
                    m pop(ok)
               IGNORE_er.
                    null

     ;;pop(in_bounds.): #of
          debug assert(in_bounds < m count())
          result: m[in_bounds]!
          m erase(in_bounds)
          result

     # returns the number of elements erased
     ;;erase(select. #select[#index, #offset]): #count
          if m in_bounds(select) is ok.
               m erase(ok)
          else
               0

     ;;erase(in_bounds.): #count
          debug assert(in_bounds < m count())
          range(in_bounds, m count() - 1) each DESTINATION_in_bounds:
               m[DESTINATION_in_bounds] = m[in_bounds: DESTINATION_in_bounds + 1]!
          _of: m pop()
          1

     # returns a copy of this array, but sorted.
     ::sort[require: #of is #orderable](): #o

     # sorts this array in place.
     ;;sort[require: #of is #orderable](): #null

     #[#
     sorts with a custom comparator that acts on a single element at a time.
     `fn(by: #of): ~#t` should be easy to calculate on the fly, otherwise
     you should run `tabulated`.  the result type `#t` should be easily
     comparable via `<`, `<=`, etc.  examples:
          ```
          array: ["abc", "de", "f", "ghij"]
          print(array sort((by:): by count()))    # prints ["f", "de", "abc", "ghij"]
          print(array sort $($by count()))        # same, but via lambda arguments
          ```
     #]#
     :;sort[require: #t is #orderable]
     (    fn(by: #of): ~#t
     ):        #respectively[#o, #null]

     #[#
     sorts using a tabulated work array (with an additional cost of
     `O(m count() * #memory(#t))`).  should be used in case `fn(by)`
     is expensive to compute.  example:
          ```
          array: ["abc", "de", "f", "ghij"]
          # should print ["f", "de", "abc", "ghij"]:
          print(array sort $($by_tabulated count())
          ```
     #]#
     :;sort[require: #t is #orderable]
     (    fn_(by_tabulated: #of): ~#t
     ):        #respectively[#o, #null]


     #[#
     sorts with a custom comparator that acts on two elements at a time.
     without certain transitive properties, the result's order may not make sense.
          ```
          array: ["abc", "de", "f", "ghij"]
          print
          (    array sort
               (    earlier(left: #str, right: #str): #ordering
                         if $left count() <= $right count()
                              ~# left
                         else
                              ~# right
               )
          )
          ```
     #]#
     :;sort(earlier(left: #of, right: #of): #ordering): #respectively[#o, #null]

     ::hash[require: #of is #hashable](~builder;): #null
          m each of:
               builder hash(of)

     #er: #CONTAINER_er
     #count: #capacity
}

[#hm, #CONTAINER_er]: \/container
