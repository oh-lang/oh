# low level data structure that is useful as a foundation for insertion-ordered
# containers that are indexed by an `at`, e.g., insertion-ordered hash maps.
# at a low level, it interleaves a doubly-linked list and a swiss table.
linked_swiss_table_
[    # key for the hash map
     at_
     # what's stored in a element at `at` in the table.
     # can be null if we're a `set_`.
     of_
     slots_per_bucket: count_ = 16
     # type used for counting elements in this table.
     count_: select_count_ = arch_ count_
     count_managed_: managed_
     # if you really want to optimize the size of this struct, we can use
     # some indirection to put `count` in an allocated spot if `on_heap`.
     # usage: `linked_swiss_table_[count_managed_ locally, ...]` or `[count_managed: _ locally, ...]`
     count_managed: _ locally
     # these also optimize the size of the struct (if `on_heap`).
     start_managed_: managed_
     start_managed: _ locally
     end_managed_: managed_
     end_managed: _ on_heap
     first_free_managed_: managed_
     first_free_managed: _ on_heap
     # maximum number of attempts to place a new element.
     # if more than this, will trigger growing the table (rehash).
     max_attempts: count_ = 2 * slots_per_bucket
     expected_load_factor: dbl_ = 0.87
]:
[    @private m:
     [    # we don't use a stack for this because we want the `count` to be stored
          # compactly (either later in this struct or in a header).
          elements; size_optimized_allocation_[element_, header_: element_header_]
          # data for the doubly-linked list.  we don't use a normal DLL because
          # we don't grow this like a stack, we index into it via hash truncation.
          # INVARIANT: each `link_bucket metadata` is always initialized.
          link_buckets; size_optimized_allocation_[link_bucket_, header_: links_header_]
          # for optimal packing, we put all counts here at the end.
          count[require: count_managed == _ locally];
          start[require: start_managed == _ locally]; dll_offset_
          end[require: end_managed == _ locally]; dll_offset_
          first_free[require: first_free_managed == _ locally]; dll_offset_
     ]
]
{    ::count_(): count_
          @if count_managed == _ locally
               m count
          @else
               # TODO: this might be cleaner:
               # `m elements header_() count ?? 0`
               # but we'd have to do something like `vector3?;`, `vector3 x` being nullish,
               # and the corresponding stuff for `hm_` as well.
               m elements header_() map_({$ok count}, {$_er, 0})

     # this is used specifically for the `elements` capacity, i.e., how large
     # the stack of elements can get without requiring a reallocation.
     ::capacity_(): capacity_

     # reserves capacity for the exact number of elements that we expect in this
     # hash table.  if less than the current number of elements, we will remove some.
     # prefer `;;capacity_(at_least.)` if the table is expected to grow over time.
     # if `update_slot_capacity` is true, we'll adjust the number of slots based on
     # `expected_load_factor`, i.e., rehash the table, but that is probably what you want.
     ;;capacity_(NEW_capacity., update_slot_capacity: true): hm_[null_]

     # reserves capacity for `>= at_least` elements that in this hash table.
     # we will use something like `max_(at_least, 2 * m capacity_())` to ensure
     # we grow in an amortized way over time.
     # this may cause a rehash of the hash table if `update_slot_capacity` is true. 
     ;;capacity_(at_least. capacity_, update_slot_capacity: true): hm_[null_]
          m capacity_(max_(at_least, 2 * m capacity_() ?? capacity_ max))

     ::slot_count_(): capacity_
          m link_buckets capacity_() * slots_per_bucket

     ::open_slot_count_(): capacity_
          m slot_count_() - m count_()

     # returns an error if there's already an element inside the table
     # with the given `at`.  can also return an error if we are out of memory,
     # but will try to expand the table if possible.
     # see also `replace_`.
     ;;append_(at., of[require: of_ is not_[null_]].): hm_[null_]
     {    if m remaining_capacity_() == 0
               m grow_()
          what m locate_(at)
               element_offset.
                    er_ at_already_present
               _ no_room
                    m grow_()
                    retry
               tombstone_location.
                    if tombstone_location attempts > max_attempts
                         m grow_()
                         retry
                    m append_[was_tombstoned: true](tombstone_location, at, of[require: of_ is not_[null_]])
               link_location.
                    if link_location attempts > max_attempts
                         m grow_()
                         retry
                    m append_[was_tombstoned: false](link_location, at, of[require: of_ is not_[null_]])
     }

     @private
     ;;append_[was_tombstoned: bool_](link_location., at., of[require: of_ is not_[null_]].): hm_[null_]
     {    dll_offset: link_location offset
          (dll_element;) = m dll_element_(dll_offset)
          debug assert_(!!m link_buckets)
          @if was_tombstoned
               m first_free_() = dll_element next
          previous_last: m end_()
          m end_() = dll_offset
          dll_element previous = previous_last
          dll_element next = dll_offset_ max
          if !previous_last is_valid_index_()
               m start_() = dll_offset

          element_offset: @if was_tombstone
               dll_element offset
          @else
               m count_()
          ptr[element_]: m elements at_(element_offset)
          # TODO: let's try to use `require: of_` and coerce `null_` to a falsy.
          ptr initialize_([at, of[require: of_ is not_[null_]])
     }

     # swaps the `of` that was at `at`, or an error if there wasn't
     # already an element inside the table with the given `at`.
     # see also `append_` and `remove_`.
     ;;swap_[require: of_ is not_[null_]](at:, of;): hm_[null_]
          what m locate_(at)
               element_offset.
                    m elements at_(element_offset) swap_(of)
               else
                    er_ no_such_at

     # returns an error if there's not already an element inside the table
     # with the given `at`, otherwise the element's `of`.
     ;;remove_(at:): hm_[of_]
          what m locate_(at)
               element_offset.
                    m elements at_(element_offset) remove_()
               else
                    er_ no_such_at

     # returns the number of elements deleted, should be 0 or 1.
     ;;delete_(at:): count_
          what m locate_(at)
               element_offset.
                    m elements at_(element_offset) deinitialize_()
                    1
               else
                    0

     # deletes any elements which match the given `all`.
     # returns the number of elements deleted.
     ;;delete_[require: of_ is not_[null_]](all: of_): count_

     # returns the number of elements deleted, should be 0 or 1.
     ;;delete_[require: of_ is not_[null_]](first: of_): count_

     # returns the number of elements deleted, should be 0 or 1.
     ;;delete_[require: of_ is not_[null_]](last: of_): count_

     # if readonly, returns an error if there's not already an element inside
     # the table with the given `at`.  if writable, returns an error if `at` is
     # not in the table and either we ran out of memory or `of_` is not defaultable.
     ;:at_[require: of_ is not_[null_]](at.): hm_[(of;:)]

     # NOTE TO SELF: we can't do `;;at_(...): (of?;)` because we'll potentially
     # swallow errors (e.g., `map at_(3) = null` should be fine from a memory standpoint,
     # but `map at_(3) = 7` may throw due to running out of memory.
     ::at_[require: of_ is not_[null_]](at.): hm_[(of?:)]

     # TODO: `;;` doesn't make sense when `of_` is `null_`.  we should split them out.
     ;:each_(do_(at:, of[require: of_];:): loop_[~t_]:)?: t_
          dll_offset; m start_()
          while dll_offset is_valid_index_()
               (dll_element;:) = m dll_element_(dll_offset)
               (element;:) = m elements at_(dll_element offset)
               # TODO: annotating nullable variables with `?` is a pain here:
               # if `t_` is `null_` then `break` would be null.  should we free
               # up `?` for other purposes, or not require it for variable declarations?
               if do_(element at, element of[require: of_]) is break.
                    return break
               dll_offset = dll_element next

     @private
     ::locate_(at:): one_of_
     [    element_offset:
          # `at` is not present in the table, but should go here if you want to add it.
          # it's a new location, i.e., on a cleared metadata.
          link_location:
          # `at` is not present in the table, but should go here if you want to add it.
          # it's at a tombstoned metadata location.
          tombstone_location:
          no_room: null_
     ]
     {    full_hash: fast_hash_(at)
          [metadatum:, remaining_hash:] = metadatum_ extract_(full_hash)
          partial_hash: remaining_hash truncate_()

          bucket_count: m link_buckets capacity_()
          slot_count: slots_per_bucket * bucket_count
          offset; full_hash % slot_count
          best_tombstone?; tombstone_location_
          slot_count each attempts: count_
               offset_metadatum: m metadatum_(offset)
               # TODO: this part can be sped up with SIMD, if available.
               if offset_metadatum cleared_()
                    if best_tombstone is tombstone_location:
                         return [tombstone_location]
                    else
                         return [link_location: [offset, attempts]]
               elif offset_metadatum filled_()
                    if offset_metadatum == metadatum
                         # there's a good chance we're already a hit.
                         dll_element: m dll_element_(offset)
                         if dll_element partial_hash == partial_hash
                              # there's a very good chance we're already a hit.
                              element_offset: dll_element offset
                              if m elements[element_offset] at == at
                                   return [element_offset]
               else
                    debug assert_(offset_metadatum tombstoned_())
                    if best_tombstone == null
                         best_tombstone = [offset, attempts]
               ++offset
          _ no_room
     }

     @private
     link_location_: [offset;, attempts: count_]
     @private
     tombstone_offset_: link_location_

     # shuffles the iteration order of this table
     ;;shuffle_(): null_

     # optimizes this table so that all elements are laid out in order
     # and the linked list goes from left to right contiguously.
     # this is not really necessary to do unless elements have been deleted,
     # or the table has been shuffled/sorted.
     # TODO: should this perform a rehash?  probably?
     ;;defrag_(): null_

     capacity_: count_
     # we want to totally distinguish `dll_offset_` and `element_offset_`
     # to avoid using one for the other accidentally.
     dll_offset_: @distinct count_ offset_
     element_offset_: @distinct count_ offset_

     @private
     ;:start_[require: start_managed == _ locally](): (dll_offset_;:)
          m start

     @private
     ;:start_[require: start_managed == _ on_heap](): (dll_offset_;:) where !!m link_buckets
          m link_buckets header_() start

     @private
     ::start_[require: start_managed == _ on_heap](): dll_offset_
          if m link_buckets
               m link_buckets header_() start
          else
               dll_offset_ max

     @private
     ;:end_[require: end_managed == _ locally](): (offset_;:)
          m end

     @private
     ;:end_[require: end_managed == _ on_heap](): (offset_;:) where !!m link_buckets
          m link_buckets header_() end

     @private
     ;:first_free_[require: first_free_managed == _ locally](): (offset_;:)
          m first_free

     @private
     ;:first_free_[require: first_free_managed == _ on_heap](): (offset_;:) where !!m link_buckets
          m link_buckets header_() first_free

     @private
     element_header_:
     [    count[require: count_managed == _ on_heap];
     ]

     @private
     link_buckets_header_:
     [    start[require: start_managed == _ on_heap]; dll_offset_
          end[require: end_managed == _ on_heap]; dll_offset_
          # index into `dll` that is the first free in the list.
          # essentially will be a SLL of the tombstoned elements.
          first_free[require: first_free_managed == _ on_heap]; dll_offset_
     ]

     @private
     link_bucket_:
     [    metadata; vector_[slots_per_bucket, metadatum_]
          # the `dll_element`s can have links to other buckets.
          dll; buffer_[slots_per_bucket, dll_element_]
     ]

     @private element_: [at:, of[require: of_ is not_[null_]];]

     @private
     dll_element_:
     [    partial_hash;
          # offsets are not based on buckets; to get the bucket index
          # you use `offset // slots_per_bucket`, and the index within
          # the bucket via `offset %% slots_per_bucket`.
          # similarly for `next` and `previous`.
          offset; element_offset_
          next; dll_offset_
          previous; dll_offset_
     ]
     @private
     partial_hash_: count_ unsigned_

     @private
     size_optimized_allocation_[of_, header_]: allocation_
     [    of_
          count_
          capacity_managed_ on_heap
          header_
     ]
}

@private
metadatum_: u8_
{    ;;renew_(): null_
          m u8 = 0

     extract_(full_hash. u64_): [metadatum:, remaining_hash:]
          m;
          remaining_hash: m fill_(full_hash)
          [m, remaining_hash]

     ::cleared_(): bool_
          m u8 == 0
     ::filled_(): bool_
          m u8 & present
     ::tombstoned_(): bool_
          m u8 == tombstone

     ;;clear_(): null_
          m u8 = 0
     ;;fill_(full_hash. u64_): [remaining_hash: u64_]
          m u8 = present | (full_hash & 127)
          remaining_hash: full_hash >> 7
     ;;tombstone_(): null_
          m u8 = tombstone

     present: u8_ = 128
     tombstone: u8_ = 127
}
