# low level data structure that is useful as a foundation for hash maps.
swiss_table_
[    # key for the hash map
     at_
     # value for the hash map
     of_
     # element to be held *in* the hash map.
     # override if your data is somewhere else.
     element_: [at:, of;]
     # if not null, this will increase the size of the `table_element`
     # held in the hash map by the size of this type.
     remaining_hash_: arch_ unsigned_
     # type used for counting elements in this table.
     count_: arch_ count_
     # override if there's a chance that storing data can fail.
     store_er_: never_
     # override if there's another way to combine `at` and `of` into an `element`.
     store_(m;, at., of.): hm_[ok_: element_, er_: store_er_]:
          [at, of]
     # override if there's another way to get `of` from `element`.
     retrieve_(m;:, element;:): (of;:)
          element of
]:
[    @private m:
     [    table; ptr_[null_]
          count;
          capacity;
     ]
]
{    new_table_(capacity.): ptr_[table_[capacity]]

     capacity_: count_

     @private
     table_element_: @if remaining_hash_ is null_
          element_
     @else
          [remaining_hash:, element;]

     split_[require: remaining_hash_ is not_[null_]](hash. u64_): [hash_start: u6_, remaining_hash:]
          [hash_start: _(truncate: hash), remaining_hash: _(truncate. hash >> 6)]

     @private
     table_[capacity]:
     [    metadata; vector_[count: capacity, metadatum_]
          elements; buffer_[count: capacity, table_element_]
     ]
     {    ;;insert_(hash: u64_, element.): u64_
               in_bounds; hash % capacity
               while m metadata[in_bounds] presence == full
                    in_bounds = (in_bounds + 1) % capacity
               @if remaining_hash_ is null_:
                    hash_start: u6_(truncate: hash)
                    m metadata[in_bounds] = [hash_start, presence: full]
                    m elements move_construct_(in_bounds, element)
               @else
                    [hash_start, remaining_hash]: split_(hash)
                    m metadata[in_bounds] = [hash_start, presence: full]
                    m elements move_construct_(in_bounds, [remaining_hash, element])
               in_bounds
     }
}

@private
metadatum_:
[    hash_start; u6_ = 0
     presence; one_of_[empty, full, deleted] = empty
]
