hm[ok, er]: one_of[ok, er]
{   # The API is `Ok: Hm assert()`, which will bubble up this `er`
    # if the result was an error.  Note that we use the `block` API
    # which normally is implicit but can be used explicitly if needed.
    #   ```
    #   do_something(Dbl): hm[ok: dbl, er: str]
    #       if Dbl < 0
    #           er("Invalid Dbl: $(Dbl)")
    #       else
    #           ok(Dbl sqrt())
    #
    #   # example with implicit `block` (like a guard/control flow):
    #   implicit_block(): hm[ok: null, er: str]
    #       # will return early if an invalid type.
    #       Result: do_something(1.234) assert()
    #       print(Result)
    #
    #   # example with explicit `block` that does the same thing as implicit:
    #   explicit_block(): hm[ok: null, er: str]
    #       indent
    #       (   fn(Block[hm[ok: null, er: str]]):
    #               Result: do_something(1.234) assert(Block)
    #               print(Result)
    #               Block exit(null)
    #       )
    #   ```
    ..assert[block_return: contains[er]](Block[~block_return]): ok
        what M!
            Ok. {Ok}
            Er.
                debug error(Er)
                Block exit(Er)

    # The API is `Ok: Hm assert(Er: "custom error if not `ok`")`.
    # This will moot `M` and shortcircuit a failure (i.e., if `Hm`
    # is `er`) to the calling block.  For example,
    #   ```
    #   do_something(Dbl): hm[ok: dbl, er: str]
    #       if Dbl < 0
    #           er("Invalid Dbl: $(Dbl)")
    #       else
    #           ok(Dbl sqrt())
    #
    #   # example with implicit `block` (like a guard/control flow):
    #   implicit_block(): hm[ok: null, er: one_of[Invalid_do_something, Other_error]]
    #       # will return early if an invalid type.
    #       Result: do_something(1.234) assert(Er: Invalid_do_something)
    #       print(Result)
    #
    #   # example with explicit `block` that does the same thing as implicit:
    #   explicit_block(): hm[ok: null, er: one_of[Invalid_do_something, Other_error]]
    #       indent
    #       (   fn(Block[hm[ok: null, er: str]]):
    #               Result: do_something(1.234) assert(Block, Er: Invalid_do_something)
    #               print(Result)
    #               Block exit(null)
    #       )
    #   ```
    ..assert[block_return: contains[new_er]](@New Er: ~new_er, Block[~block_return]): ok
        what M!
            Ok. {Ok}
            Er.
                debug error(Er)
                Block exit(@New Er)

    # maps an `Ok` result to a different type of `ok`, consuming `M`.
    # since this is a `@Named` namespace, we need to use `My_hm map((Ok.): Ok + 3)`
    # instead of whatever type `ok` is, e.g., `My_hm map((Int.): Int + 3)` would not work
    # even if `ok` is type `int`.
    ..map(fn(@Named Ok.): ~new_ok): hm[ok: new_ok, er]

    # passes through any `Ok` result, but maps an `Er` to the desired `ok` result
    # via the passed-in function.  while this looks like it could be indistinguishable
    # from the next method if types `ok` and `er` are the same, the return type here
    # is just `ok`, so we can distinguish based on return type overloading.
    ..map(fn(@Named Er.): ok): ok

    # maps an `Er` result to a different type of `er`.
    ..map(fn(@Named Er.): ~new_er): hm[ok, er: new_er]

    # TODO: do we really want a function like this when we can have people
    # do `what M! { Ok. {...ok logic...}, Er. {...er logic...}}`?
    # TODO: do we even want any `map` methods since we have `what`?
    # maps `ok` and `er` separately.
    ..map(fn(@Named Ok.): ~t, fn(@Named Er.): t): t

    # runs a callback if OK but returns self
    ..check(fn(@Named Ok): null): m

    # runs a callback if not OK but returns self
    ..check(fn(@Named Er): null): m

    # runs different callbacks based on OK/not-OK but returns self
    ..check(fn(@Named Ok): null, fn(@Named Er): null): m

    # TODO: should we use `to` here or is there a better way to indicate casting?
    # it's technically something like `one_of[ok, null](Result: hm[ok, er]): one_of[ok, null]`
    # which is pretty verbose.  could use `(M.): one_of[ok, null]` for implicit conversion
    ..to(): one_of[ok, null]
}
