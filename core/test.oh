# TODO: define the `@test_panics` macro
# TODO: define the `@compile_panics` macro

@test(Ast;): hm[er: str]
    @Bad_declaration Er: 'Expected `@test "test name":` with `{...}` or indented block'
    Node; Ast parse_next() assert(@Bad_declaration Er)

    # TODO: maybe `binary_operation` instead of `binary_operation`?  or `op_left_right`
    Binary; Node! binary_operation() assert(@Bad_declaration Er)
    assert(Binary operator == Op_declare_readonly, @Bad_declaration Er)

    # `Test_name_string` includes quotes, e.g., `"whatever \"test name\" you want"`:
    Test_name: Binary Left! string() assert(@Bad_declaration Er)
    Test_block; Binary Right! block() assert(@Bad_declaration Er)

    # TODO: should we have a `continue_if Ast includes(Tests)` function?
    if !Ast includes(Tests)
        return

    # Create a unique name for the test block so we can use it in code.
    Test_block_name: Ast unique_variable()
    Ast add
    (   ast statement
        (   ast binary_operation
            (   Left: identifier(Test_block_name)
                Op_declare_readonly
                Right: Test_block
            )
        )
    )

    # TODO: it might be cleaner to just define this statement:
    # Block; block
    # (   ast statement
    #     (   ast binary_operation
    #         (   Left: ast binary_operation
    #             (   Left: identifier("Test")
    #                 Op_access
    #                 Right: identifier("run")
    #             )
    #             Op_access
    #             Right: parentheses
    #             (   ...
    #                 Test_block
    #             )
    #         )
    #     )
    # )
    Block; block
    (   ast statement("Test run(${Test_name}, ${Test_block_name})"))
    )

    # TODO: i think we need to rename to `Context` OR `State`.
    # alternatively we need to rename `test()` (the function) to `assert()`
    # or something similar (maybe `Test[X] == Y`).  due to how we disallow
    # shadowing, and that we require `lower_snake_case` and `Initial_upper_snake_case`
    # to be type versions of each other, we can't have `Test` and `test`
    # mean different things.
    if Ast in_scope("Test")
        # TODO: for running tests in parallel with the same parent state,
        # we probably could fork here.  but that's a bit hacky on Windows.
        Ast add(ast statement(Block!))
        return

    # statement, via string, builds something like this:
    # ast statement
    # (   ast binary_operation
    #     (   Left: identifier("Test")
    #         Op_declare_writable
    #         Right: identifier("test_context")
    #     )
    # )
    # `
    Block prepend(ast statement("Test; test_context"))

    Ast add(Test. Block!)

@protected
test_context: [@protected Printed; array[str], @protected Name; str]
{   ;;run(Str, Block[~value]): value
        (String): Str strip()
        M push(String)
        Value: Block run()
        M pop(String)
        Value
        # TODO: check `Value` for being OK and not an error.
        # that is, if we're in a normal test.  in a test_panics, check for error.

    @private
    ;;push(String):
        M Name &= String

    @private
    ;;pop(String):
        assert(M Name ends_with(String)) ?? panic("test_context needs to match push/pop")
        M Name count(M Name count() - String count())
        if M Name ends_with(' ')
            M Name pop()
}
        

test[of]: [Of, Oh_line]
{   g(~T., @Oh_line): g[t]
        [T, Oh_line]

    ::==(Of): bool
        if M Of == Of
            return True
        error("${M Oh_line}: expected ${M Of} == ${Of}")
        panic("expected ${M Of} == ${Of}")
        False

    ::!=(Of): bool
        if M Of != Of
            return True
        error("${M Oh_line}: expected ${M Of} != ${Of}")
        panic("expected ${M Of} != ${Of}")
        False

    ::>=(Of): bool
        if M Of >= Of
            return True
        error("${M Oh_line}: expected ${M Of} >= ${Of}")
        panic("expected ${M Of} >= ${Of}")
        False

    ::<=(Of): bool
        if M Of <= Of
            return True
        error("${M Oh_line}: expected ${M Of} <= ${Of}")
        panic("expected ${M Of} <= ${Of}")
        False
        
    ::>(Of): bool
        if M Of > Of
            return True
        error("${M Oh_line}: expected ${M Of} > ${Of}")
        panic("expected ${M Of} > ${Of}")
        False

    ::<(Of): bool
        if M Of < Of
            return True
        error("${M Oh_line}: expected ${M Of} < ${Of}")
        panic("expected ${M Of} < ${Of}")
        False
}

@test "==":
    @test "works for ints":
        test(5) == 5
        test(10) == 10

    @test_panics "expected 5 == 6":
        test(5) == 6

@test "!=":
    @test "works for ints":
        test(5) != 50
        test(10) != 12

    @test_panics "expected 3 != 3":
        test(3) != 3

@test ">=":
    @test "works for ints":
        test(5) >= 3
        test(4) >= 4
        test(10) >= 10

    @test_panics "expected 5 >= 6":
        test(5) >= 6

@test "<=":
    @test "works for ints":
        test(5) <= 6
        test(7) <= 7
        test(12) <= 12

    @test_panics "expected 7 <= 6":
        test(7) <= 6

@test "<":
    @test "works for ints":
        test(5) < 6
        test(10) < 11

    @test_panics "expected 7 < -1":
        test(7) < -1

    @test_panics "expected -1 < -1":
        test(-1) < -1

@test ">":
    @test "works for ints":
        test(5) > 3
        test(20) > 19

    @test_panics "expected -1 > 1":
        test(-1) > 1

    @test_panics "expected 1 > 1":
        test(1) > 1
