regular_file[Mode]: [@private File_handle]
{   m(Regular_file_path:): hm[m]

    ;;descope(): null

    # moves the file pointer to the byte specified by `Count`.
    ;;seek(Count): hm[null]

    # the number of bytes in this file.
    ::count(): count

    # reads the remaining parts of the file as an array of bytes.
    # if you just opened the file, this will read the entire file.
    # if you are at the end of a file, it will return an empty array.
    ;;read[Require: Mode is_readable()]: hm[array[u8]]

    # reads a line from the file, stripping newline character(s).
    # if you are at the end of the file, will return a null line.
    # an empty line will be the empty string.
    ;;read[Require: Mode is_readable()]: hm[[Line?: str]]

    # reads all remaining lines from the file, stripping newline character(s)
    # and putting each line into a separate element in the array.  an empty
    # line will be an empty string.  if you are at the end of a file,
    # it will return an empty array.
    ;;read[Require: Mode is_readable()]: hm[[Lines: array[str]]]

    # appends all lines to the file, and also adds newline character(s).
    # TODO: update to `iterator[str]` instead of `array[str]`
    ;;write[Require: Mode is_writable()](Lines: array[str]): hm[null]
        Lines each Line:
            M write(Line)

    # appends a line to the file, and also adds newline character(s).
    ;;write[Require: Mode is_writable()](Line: str): hm[null]

    # appends bytes to the file.
    ;;write[Require: Mode is_writable()](Array[u8]): hm[null]

    # appends a byte to the file.
    ;;write[Require: Mode is_writable()](U8): hm[null]
}

mode: one_of
[   Read_only
    Read_write
    # file is created or truncated if already present
    Truncate_write
]
{   ::is_writable(): bool
        what M
            Read_write
                True
            Truncate_write
                True
            Read_only
                False

    @alias is_writeable: is_writable

    ::is_readable(): bool
        what M
            Read_only
            Read_write
                True
            Truncate_write
                False
}

[regular_file_path]: \/path
[er, hm]: \/../file
