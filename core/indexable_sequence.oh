[in_bounds]: oh("./allocation.oh")

indexable_sequence[of: not[null]]: container[of, at: index]
{   ;;count(@Resize_to Count): null 

    # requires a positive (or zero) index passed in.
    # will return an error if `In_bounds >= M count()`.
    :;[In_bounds.]: hm[(Of:;)]

    # in-bounds reference, which is unsafe to call unless `In_bounds` is 
    # within the range `[0, M count())`.
    @can_panic
    :;[In_bounds.]: (Of:;)

    # nullable getter; returns null if `Index` is outside the bounds of the array.
    ::[Index.]: (Of?)
        refer
        (   M
            At. @hide Index
            fn(O, Index.): (Of?:)
                In_bounds: O in_bounds(Index) ?? return (Of?: Null)
                (Of: O[In_bounds])
        )

    # non-nullable getter/setter; resizes the array if necessary.
    ;;[Index.]: @hm_or_panic[(Of;)]
        refer
        (   M
            At. @hide Index
            fn(O;, Index.): @hm_or_panic[(Of;)]
                In_bounds: O in_bounds(Index) assert()
                (Of; O[In_bounds])
        )

    @protected
    ::in_bounds(Index.): hm[in_bounds]
        M ensure_non_negative(Index;) assert()
        assert(Index < count(), Er: Invalid_index)
        ok(in_bounds(Index))

    # Resizes the array to make `Index` in bounds, as long as it's positive.
    # Can error out if `Index` is too negative or array expansion failed.
    @protected
    ;;in_bounds(Index.): hm[in_bounds]
        M ensure_non_negative(Index;) assert()
        if Index >= count()
            count(Index + 1) assert()
        ok(in_bounds(Index))

    # TODO: `ensure_non_negative`
}
