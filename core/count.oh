#[#
we leave room for negative indexing, so a `u8_` count is only valid
from 0 to 128, so all signed `i8_` indexes from -128 to 127 are valid
(and there is no space for an invalid index), i.e., via `count_ index_`.
we also have concepts of an `count_ offset_` which is an unsigned
index that is valid from 0 to 127 for a `u8_` count.
#]#
select_count_: select_{count64_:, count32_:, count16_:, count8_:}

count_arch_: count_{arch_ unsigned_}

count64_: count_{u64_}
count32_: count_{u32_}
count16_: count_{u16_}
count8_: count_{u8_}

@final
count_{of_: select_unsigned_}: all_of_
{    m: [@private value; of_]
     integer;
}
{    m_(of.): hm_{m_}
          if of <= max_() value
               [value. of]
          else
               unrepresentable

     m_(signed{of_}.): hm_{m_}
          [value: of_(signed) assert_()]

     ?:
     {    ::is_(null_): bool_
               m value > max_() value

          null: [value: max_() value + 1]

          :;null_boxing_(): (of:;)
               m value
     }

     @integer(m value)

     min_(): of_
          0

     max_(): of_
          # should be 128 for `of_: u8_`
          of_ max_() // 2 + 1

     ["min_", "max_"] @each method: str_
          @concat(method, "_")(): m_
               [value: _ @concat(method, "_")()]

     ::double_or_max_(): m_
          if m value < max_() value // 2
               [value: m value * 2]
          else
               max_()

     index_: index_{signed_{of_}}
     offset_: offset_{of_}

     hm_{of_:}: hm_{ok_: of_, number_ er_}
}

@test "min_"
     assert_(count8_ max_()) == 0
     assert_(count16_ max_()) == 0
     assert_(count32_ max_()) == 0
     assert_(count64_ max_()) == 0

@test "max_"
     assert_(count8_ max_()) == 128
     assert_(count16_ max_()) == 32_768
     assert_(count32_ max_()) == 2_147_483_648
     assert_(count64_ max_()) == 9_223_372_036_854_775_808

@test "double_or_max_"
     @test "count8_"
          c_: count8_
          assert_(c_(0) double_or_max_()) == 0
          assert_(c_(3) double_or_max_()) == 6
          assert_(c_(63) double_or_max_()) == 126
          assert_(c_(64) double_or_max_()) == 128
          assert_(c_(65) double_or_max_()) == 128
          assert_(c_(65) double_or_max_()) == 128
          assert_(c_(127) double_or_max_()) == 128
          assert_(c_(128) double_or_max_()) == 128

@test "index and offset"
     @test "count8_"
          c_: count8_
          assert_(c_ index_) == index_{i8_}
          assert_(c_ offset_) == offset_{u8_}
