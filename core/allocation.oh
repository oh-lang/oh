@final
allocation_
[    of_
     # if not null, we allocate just enough space for this extra data
     # just before the buffer of `of_` values.  there can be multiple
     # `of` values, but only ever one instance of `header` is added.
     header_: any_ = null_
     # determines the maximum size of the allocation, and
     # makes indexing this allocation potentially more efficient.
     count_: select_count_
     # use an alias so we can do, e.g., `allocation_[capacity_managed_ on_heap]`
     # to specify `capacity_managed` (see next field).
     capacity_managed_: managed_
     # whether to put capacity locally inside this allocation.
     # if `on_heap`, it gets added to the `full_header_`
     capacity_managed:
]:
[    @private m:
     [    of_ptr[of_];
          capacity[require: capacity_managed == locally];
     ]
]
{    ::capacity_(): capacity_
          @if local_capacity
               m capacity
          @else
               m full_head_() capacity

     ::!(): bool_
          m of_ptr == null

     # ensure you free any elements that were initialized
     # if the new capacity is smaller than the old capacity.
     # this data structure does not keep track of which elements are initialized.
     ;;capacity_(new. capacity_): hm_[null_]
          m capacity_(new, old. m capacity_())

     @private
     ;;capacity_(new. capacity_, old. capacity_): hm_[null_]
          required_bytes: memory_ = assert_
          (    new * of_ memory_() + full_header_bytes
               er: out_of_memory
          )
          if old == 0
               ptr[u8_]: required_bytes allocate_() assert_(er: out_of_memory)
               m full_head_(ptr)
          else
               ptr[u8_]; m full_head_()
               required_bytes reallocate_(;ptr) assert_(er: out_of_memory)
               m full_head_(ptr)

     # make sure that this element is already initialized;
     # this `allocation` does not keep track of which elements are initialized.
     # this will return an error if `offset` is out of bounds.
     ;:at_(offset.): hm_[(of;:)]
          assert_(offset < m capacity_())
          to_(m of_ptr + offset)

     # make sure that this element is NOT already initialized;
     # this `allocation` does not keep track of which elements are initialized.
     ;;initialize_(offset., of.): hm_[null_]
          assert_(offset < m capacity_())
          ptr_offset: ptr_[of_] = m of_ptr + offset
          memory_ initialize_(of, ptr_offset)

     # make sure that this element is already initialized;
     # this `allocation` does not keep track of which elements are initialized.
     ;;deinitialize_(offset.): hm_[null_]
          assert_(offset < m capacity_())
          ptr_offset: ptr_[of_] = m of_ptr + offset
          memory_ deinitialize_(ptr_offset)

     # make sure that this element is already initialized;
     # this `allocation` does not keep track of which elements are initialized.
     ;;remove_(offset.): hm_[of_]
          assert_(offset < m capacity_())
          ptr_offset: ptr_[of_] = m of_ptr + offset
          memory_ remove_(ptr_offset)

     # TODO: non-local_capacity versions of the above, call them.
     ::header_[require: header_ is not_[null_]](): hm_[(header:)]
          if m
               ok_(m header_())
          else
               er_(unallocated)

     ;;header_[require: header_ is not_[null_]](): hm_[(header;)]
          if !m
               m capacity_(small_capacity) assert_()
          assert_(!!m)
          ok_(m header_())

     # gets the `header`, when `m` is truthy.
     ;:header_[require: header_ is not_[null_]](): (header;:) where !!m
          (full_header;:) = m full_head_() to_()
          full_header header

     @private
     has_full_header_():
          header_ is not_[null_] or capacity_managed == on_heap

     @private
     ;;full_head_(ptr[u8_].): null_
          if ptr == null
               m of_ptr = null
          else
               m of_ptr = ptr + full_header_bytes

     @private
     ::full_head_[require: full_header_ is not_[null_]](): ptr_[u8_] where !!m
          ptr[u8_]: m of_ptr  # TODO: casting??
          ptr -= full_header_bytes  # `full_header_` starts before the `of_` buffer
          ptr

     @private
     ::full_head_(): ptr_[u8_] where !m
          ptr_()    # null pointer

     @private
     full_header_bytes: memory_
          @if full_header_ is null_
               0
          @else
               at_least_memory: memory_(full_header_)
               word_memory: memory_(arch_ word_)
               # TODO: maybe just `ceil(at_least_memory / word_memory) * word_memory`??
               need_one_more_word: at_least_memory %% word_memory != 0
               (at_least_memory // word_memory + need_one_more_word) * word_memory


     # TODO: update based on size of `of_`
     small_capacity: 4

     capacity_: count_
     offset_: offset_[count_]

     @if header_ is not_[null_] or capacity_managed == on_heap
          @private full_header_:
          [    header[require: header_ is not_[null_]];
               capacity[require: capacity_managed == on_heap];
          ]
     @else
          @private full_header_: null_

     hm_[of_]: hm_[of_]
}

hm_[of_]: hm_
[    ok_: of_
     er_: container_ er_
]
